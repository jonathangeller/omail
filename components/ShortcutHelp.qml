import QtQuick
import qs.Commons
import qs.Ui
import "../keys/Keymap.js" as Keymap

// The reference sheet behind Ctrl+?. A plain list rather than a dialog because
// it never needs an answer — Esc, Ctrl+? again, or a click puts it away.
Rectangle {
  id: root

  required property color textColor
  required property color backgroundColor
  required property color dimColor
  required property string panelFontFamily

  signal dismissed()

  // From the table, so this sheet cannot drift from what the keys actually do.
  // It used to be written by hand, and had: Esc listed twice, no `u` and no
  // `?`, and "Right-click a row" among the keyboard shortcuts.
  readonly property var groups: Keymap.helpGroups()

  color: Qt.rgba(backgroundColor.r, backgroundColor.g, backgroundColor.b, 0.96)

  MouseArea {
    anchors.fill: parent
    onClicked: root.dismissed()
  }

  Column {
    anchors.centerIn: parent
    width: Math.min(parent.width - Style.space(80), Style.space(460))
    spacing: Style.space(6)

    Text {
      text: "Keyboard shortcuts"
      color: root.textColor
      font.family: root.panelFontFamily
      font.pixelSize: Style.font.subtitle
      font.bold: true
    }

    Item {
      width: parent.width
      implicitHeight: Style.space(6)
    }

    Repeater {
      model: root.groups

      Column {
        id: group
        required property var modelData
        width: parent.width
        spacing: Style.space(6)

        Item {
          width: parent.width
          implicitHeight: Style.space(8)
        }

        Text {
          text: group.modelData.name
          color: root.dimColor
          font.family: root.panelFontFamily
          font.pixelSize: Style.font.caption
          font.bold: true
        }

        Repeater {
          model: group.modelData.rows

          Item {
            required property var modelData
            width: group.width
            implicitHeight: Style.space(20)

            Text {
              anchors.left: parent.left
              anchors.verticalCenter: parent.verticalCenter
              width: Style.space(178)
              text: modelData.keys
              color: root.textColor
              font.family: root.panelFontFamily
              font.pixelSize: Style.font.caption
              elide: Text.ElideRight
            }

            Text {
              anchors.left: parent.left
              anchors.leftMargin: Style.space(183)
              anchors.right: parent.right
              anchors.verticalCenter: parent.verticalCenter
              text: modelData.action
              color: root.dimColor
              font.family: root.panelFontFamily
              font.pixelSize: Style.font.caption
              elide: Text.ElideRight
            }
          }
        }
      }
    }
  }
}
